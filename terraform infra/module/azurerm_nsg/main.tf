resource "azurerm_network_security_group" "asmit22" {
  for_each            = var.nsg
  name                = each.value.name
  location            = each.value.location
  resource_group_name = each.value.resource_group_name

  dynamic "security_rule" {
    for_each = each.value.security_rules

    content {
      name                       = security_rule.value.name
      priority                   = security_rule.value.priority
      direction                  = security_rule.value.direction
      access                     = security_rule.value.access
      protocol                   = security_rule.value.protocol
      source_port_range          = security_rule.value.source_port_range
      destination_port_range     = security_rule.value.destination_port_range
      source_address_prefix      = security_rule.value.source_address_prefix
      destination_address_prefix = security_rule.value.destination_address_prefix
    }
  }
}

resource "azurerm_network_interface_security_group_association" "assoc" {
  for_each = { for k, v in var.nsg : k => v if lookup(v, "nic_name", null) != null && lookup(v, "nic_name", "") != "" }

  network_interface_id      = data.azurerm_network_interface.nic[each.key].id
  network_security_group_id = azurerm_network_security_group.asmit22[each.key].id
}

resource "azurerm_subnet_network_security_group_association" "subnet_assoc" {
  for_each = { for k, v in var.nsg : k => v if lookup(v, "subnet_name", null) != null && lookup(v, "subnet_name", "") != "" }

  subnet_id                 = data.azurerm_subnet.nsg_subnet[each.key].id
  network_security_group_id = azurerm_network_security_group.asmit22[each.key].id
}